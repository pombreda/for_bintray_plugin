buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.9.2'
        classpath 'com.github.dcendents:android-maven-plugin:1.0'
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:2.2.3'
    }
}

import java.text.SimpleDateFormat

def globalVersion = new Version(currentVersion)

// Define the artifacts coordinates
group = 'org.jfrog.example.android'
version = globalVersion
status = version.status

// Plugins
apply plugin: 'android-library'
apply plugin: 'artifactory'

// We need the patched maven plugin since 'install' task is overriden by 'installDebugTest', see: https://github.com/dcendents/android-maven-plugin
apply plugin: 'android-maven'

// Android
android {
    compileSdkVersion 19
    buildToolsVersion "19.0.3"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 19
    }
}

configurations {
  published
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java
    classifier "sources"
}

artifactoryPublish {
    dependsOn sourceJar
}

artifacts {
    published sourceJar
}

configure(install.repositories.mavenInstaller) {
        pom.project {
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
        }
    }

artifactory {
    contextUrl = 'http://localhost:8080/artifactory'
    publish {
        repository {
            repoKey = 'libs-snapshot-local' //The Artifactory repository key to publish to
            username = artifactory_user //The publisher user name, property taken from gradle.properties
            password = artifactory_password //The publisher password, property taken from gradle.properties
        }
        defaults {
            publishConfigs('archives', 'published')
            properties = ['build.status': "$it.project.status".toString()]
            publishPom = true //Publish generated POM files to Artifactory (true by default)
            publishIvy = false //Publish generated Ivy descriptor files to Artifactory (true by default)
        }
    }
    resolve {
        repository {
            repoKey = 'libs-release' //The Artifactory (preferably virtual) repository key to resolve from
            username = artifactory_user //Optional resolver user name (leave out to use anonymous resolution), property taken from gradle.properties
            password = artifactory_password //The resolver password, property taken from gradle.properties

        }
    }
}

repositories {
    jcenter()
}

// Our project dependencies
dependencies {
    compile 'joda-time:joda-time:2.3'
    
    // Backward compatibility for andoird <http://developer.android.com/tools/support-library/index.html>  
    //compile 'com.android.support:support-v4:19.1.+'
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.11'
}

class Version {
    String thisVersion
    String status

    Version(String versionValue) {
        thisVersion = versionValue
        if (thisVersion.endsWith('-SNAPSHOT')) {
            status = 'integration'
        } else {
            status = 'release'
        }
    }

    String toString() {
        thisVersion
    }
}
